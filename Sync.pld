GAL16V8
Sync

Clock   NRST   HBPulse  HEPulse VBPulse VEPulse HCEND  VCEND  HVisEnd GND
NOE     VVisEnd  HSync  VSync   HVis    VVis    Vis    NC     NC      VCC

; Sync pulses are negative
; (which means that in reset we definitely want a high output)

; Horizontal sync:
;   - if /NRST is asserted, next HSync should be high
;   - if HEPulse is asserted, next HSync should be high
;   - if neither HBPulse nor HEPulse is asserted, next HSync value
;     should be the same as the current one
HSync.R =   /NRST
          + HEPulse
          + /HBPulse * /HEPulse * HSync

; Vertical sync:
;   - if /NRST is asserted, next VSync should be high
;   - if VEPulse is asserted, next VSync should be high
;   - if neither VBPulse nor VEPulse is asserted, next VSync value
;     should be the same as the current one
VSync.R =   /NRST
          + VEPulse
          + /VBPulse * /VEPulse * VSync

; Horizontal visibility:
;   - true on reset or when HCOUNT_END is asserted
;   - if HVisEnd is not asserted, next HVis value should
;     be the same as the current one
HVis.R  =   /NRST
          + HCEND
          + /HVisEnd * HVis

; Vertical visibility:
;   - next VVis true on reset
;   - next VVis true if HCOUNT_END & VCOUNT_END (end of frame)
;   - if VVisEnd is not asserted, next VVis value should be
;     the same as the current one
;   - if VVisEnd is asserted and HCOUNT_END is not asserted,
;     next VVis value should be the same as the current one
VVis.R  =   /NRST
          + HCEND * VCEND
          + /VVisEnd * VVis
          + VVisEnd * /HCEND * VVis

; Current pixel visibility
Vis     = HVis * VVis

DESCRIPTION

HSync, VSync, and pixel visibility signal generation.

Inputs (active-high unless otherwise noted):

  NRST     active-low reset signal
  HBPulse  a HSync pulse should start
  HEPulse  the HSync pulse should end
  VBPulse  a VSync pulse should start
  VEPulse  the VSync pulse should end
  HCEND    end of horizontal count (HCOUNT=799)
  VCEND    end of vertical count (VCOUNT=524)
  HVisEnd  end of horizontal visible region (HCOUNT=639)
  VVisEnd  end of vertical visible region (VCOUNT=479)
  NOE      active-low output enable

Outputs:

  HSync    horizontal sync
  VSync    vertical sync
  HVis     current pixel is in the visible horizontal region
  VVis     current pixel is in the visible vertical region
  Vis      current pixel is visible (HVis & VVis)

; vim:ft=text:
